# Maintainer: Jeroen Ooms <jeroen@berkeley.edu>

_realname=r-base
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.5.0
pkgrel=1
pkgdesc="Software environment for statistical computing (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.r-project.org/'
license=('GPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "texinfo"
             "texinfo-tex"
             "unzip")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-omp"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
checkdepends=("diffutils"
              "unzip")
source=("https://cran.r-project.org/src/base/R-4/R-4.5.0.tar.gz"
        "wrap.sh")
sha256sums=('SKIP'
            'SKIP')
noextract=(R-4.5.0.tar.gz)

prepare() {
  # tarbal has bad symlinks, extract manually
  rm -rf ${srcdir}/${_realname}-${pkgver}
  mkdir ${srcdir}/${_realname}-${pkgver}
  MSYS="winsymlinks:lnk" tar -xf "R-4.5.0.tar.gz" -C ${srcdir}/${_realname}-${pkgver} --strip-components=1
  cd ${srcdir}/${_realname}-${pkgver}
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}/src/gnuwin32
  if [[ ${MSYSTEM} == CLANG* ]]; then
    echo "USE_LLVM = 1" >> MkRules.local
  fi
  make all
  make cairodevices
  make recommended
}

check() {
  export MY_TCLTK="${MINGW_PREFIX}/lib"
  cd ${srcdir}/${_realname}-${pkgver}/src/gnuwin32
  
  # This takes forever
  # make check || true
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}/src/gnuwin32/installer
  sed -i '/manual/d' Makefile
  sed -i '/GW_INFO_FILES/d' Makefile
  sed -i '/dosfiles.zip/d' Makefile
  make imagedir
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,libexec}
  cp -R R-4.5.0 ${pkgdir}${MINGW_PREFIX}/libexec/R
  cp ${srcdir}/wrap.sh ${pkgdir}${MINGW_PREFIX}/bin/R
  cp ${srcdir}/wrap.sh ${pkgdir}${MINGW_PREFIX}/bin/Rscript
}
